plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

configurations {
    shaded
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // fastjson
    implementation "com.alibaba.fastjson2:fastjson2:2.0.44"
    shaded "com.alibaba.fastjson2:fastjson2:2.0.44"

    // lombok
    compileOnly "org.projectlombok:lombok:1.18.30"
    annotationProcessor "org.projectlombok:lombok:1.18.30"

    // kafka
    implementation "org.apache.kafka:kafka-clients:3.6.1"
    shaded "org.apache.kafka:kafka-clients:3.6.1"

    // log4j for dev
    implementation "org.apache.logging.log4j:log4j-api:2.22.1"
    implementation "org.apache.logging.log4j:log4j-core:2.22.1"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.22.1"

    // slf4j-nop for prod
    shaded "org.slf4j:slf4j-nop:2.0.11"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 16
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

jar {
    finalizedBy remapJar
}

shadowJar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }

    configurations = [project.configurations.shaded]
    archiveClassifier = "shadow"
}

remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile
    archiveClassifier = null
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
